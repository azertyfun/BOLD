; *************************
; Project: GRUB
; File: drivers.dasm
; *************************

; ======================================================================
; This is only the needed drivers. Not anything else. I AM NOT AN OS ^^
; ======================================================================
		
;;;FONCTION write (0x1)
;;;DESCRIPTION Writes the text in arguments
;;;ARG B Address of the string
;;;ARG I Where to write (begins at 0x8000)
;;;If Z = 0xABAB, then writing in highlighted mode.
:write
	set PUSH, I
	:write_loop0
		set [I], [B]
		ifn Z, 0xABAB
			bor [I], [color]
		ife Z, 0xABAB
			bor [I], [color_hl]
		add I, 1
		add B, 1
		ifn [B], 0
			set PC, write_loop0
	
	set I, POP
	set PC, POP

:detectPeriphs
	HWN I
	SET J, 0
	:detectPeriphs_loop
		HWQ J
		IFE A, 0xf615
			SET [monitor], J
		IFE A, 0x7406
			SET [keyboard], J
		IFE A, 0x24C5
			SET [floppyDrive], J
		ADD J, 1
		IFN J, I
			SET PC, detectPeriphs_loop
		
	SET PC, POP

:clear
	set PUSH, I
	SET I, 0x8000
	:clear_loop
		SET [I], 0xF020
		ADD I, 1
		IFN I, 0x8180
			set PC, clear_loop
	SET I, POP
	set PC, POP

; =======================
; A : Start
; B : End
; C : Address to copy to
; =======================
	
:memcpy
	set [C], [A]
	add A, 1
	add C, 1
	ifn A, B
		set PC, memcpy
	set PC, POP

:monitor DAT 0
:keyboard DAT 0
:floppyDrive DAT 0

:color DAT 0xF000
:color_hl DAT 0x0F00